This is the original MS-DOS version.

1 COMMON FC%, BC%, CFLAG%: T$=TIME$:T2$=RIGHT$(T$,2):T$=MID$(T$,4,2):RANDOMIZE (1+VAL(T$))*60+1+VAL(T1$)
10 SCREEN 0,0,0: FC%=7: BC%=0: COLOR FC%, BC%, BC%: CLS
20 DEF SEG=0: CFLAG%=(PEEK(&H410)=109)
30 WIDTH 80: KEY OFF
100 ON ERROR GOTO 0: T$=TIME$:T2$=RIGHT$(T$,2):T$=MID$(T$,4,2):RANDOMIZE (1+VAL(T$))*60+1+VAL(T1$)
110 DEFINT A-Z: KEY OFF
120 DEF FNI(V) = (V=CDRVACE OR V=CPPROOF OR V=CXTANK OR V=CROWAY)
130 DIM A$(19),D$(19),A(5),D(100),I(101),C(7),P(7)
140 C25=1:C50=2:C75=3:C100=4:C200=5:COOGAS=6:CGAS=7:CXTANK=8:CFLAT=9:CSPARE=10:CPPROOF=11:CACCIDENT=12:CREPAIRS=13:CDRVACE=14:CSLIM50=15:CEOLM=16:CSTOP=17:CROLL=18:CROWAY=19:F=C25
150 FOR I=0 TO 19: A$(I) = SPACE$(14): NEXT I: FOR I=C25 TO 19: READ X$: RSET A$(I)=X$: NEXT I
160 DATA "25", "50", "75", "100", "200", Out of Gas, Gasoline, Extra Tank, Flat Tire, Spare Tire, Puncture-Proof, Accident, Repairs, Driving Ace, Speed Limit 50, End of Limit, Stop, Roll, Right-of-Way
170 FOR I=C25 TO CROWAY: FOR J=C25 TO CDRVACE: IF MID$(A$(I),J,1)<>" " THEN 190
180 NEXT J
190 T$=RIGHT$(A$(I),15-J):IF INSTR("AEO",LEFT$(T$,1)) THEN D$(I)="n "+T$ ELSE D$(I)=" "+T$
200 NEXT I:FOR J=C25 TO 5: A(J)=VAL(A$(J)):NEXT J
210 CLS:PRINT "Initializing... ";: GOSUB 770: PRINT "Shuffling... ";: GOSUB 810: PRINT "Dealing.":GOSUB 840
220 IF F=0 THEN LOCATE 10,C25: PRINT "Computer played a"D$(P1)
230 IF FNI(P1) THEN FOR Z1=0 TO 1250: NEXT Z1: GOTO 710
240 IF C3=700 THEN T$="C": GOTO 1660
250 GOSUB 870: IF T<0 THEN 760
260 P(7)=T
270 GOSUB 1770
280 LOCATE CROWAY,C25: PRINT "[ Number of cards left is"Q0-D4"]"
290 LOCATE 15,C25: PRINT "You drew a"D$(P(7))" (7)"+SPACE$(12): F=C25
300 DEF SEG: POKE 106,0: IF LEN(INKEY$) THEN 300 ' clear buffer
310 LOCATE 20,C25:PRINT SPACE$(79);SPACE$(79): LOCATE 21,C25: PRINT "Action to take ? ";
320 GOSUB 1760: IF INSTR("DPC", V$)=0 THEN BEEP: LOCATE 20,C25: PRINT "(Must type D, P, or C)":LOCATE 21,CROLL: GOTO 320
330 IF V$="" THEN BEEP: GOTO 310
340 LOCATE 21,C25
350 IF V$="P" THEN PRINT "Play "; ELSE IF V$="D" THEN PRINT "Discard "; ELSE PRINT "Coup fourr"CHR$(130)" ";
360 PRINT "which card ? ";
370 V1$=INPUT$(C25):IF V1$=CHR$(27) THEN 290 ELSE IF V1$<"1" OR V1$>"7" THEN BEEP: XP=POS(0):YP=CSRLIN: LOCATE 20,1: PRINT "(Must be from 1 to 7) ": LOCATE YP,XP: GOTO 370
380 PRINT V1$: C1=VAL(V1$)
390 I=C1: C1=P(C1)
400 IF V$<>"C" THEN 500
410 IF I=7 THEN 460
420 IF C1=CROWAY THEN IF P1<>CSTOP AND P1<>CSLIM50 THEN 460 ELSE P8=C25: GOTO 470
430 IF C1=CXTANK THEN IF P1<>COOGAS THEN 460 ELSE P9=C25: GOTO 470
440 IF C1=CPPROOF THEN IF P1<>CFLAT THEN 460 ELSE P7=C25: GOTO 470
450 IF C1=CDRVACE THEN IF P1<>CACCIDENT THEN 460 ELSE P6=C25: GOTO 470
460 LOCATE 20,C25: PRINT "(I can't allow that - try a different tack)": FOR I=C25 TO 2750: NEXT: GOTO 310
470 P4 = P4 + C25: P5 = P5 + 1
480 IF C1=CROWAY THEN IF R1<>0 THEN R1=0: IF P0=CACCIDENT OR P0=COOGAS OR P0=CFLAT THEN 680
490 P0=CROLL: GOTO 680
500 IF V$<>"P" THEN 670
510 IF C1<=C200 THEN IF (P0<>CROLL OR (C1>C50 AND R1<>0) OR (C1=C200 AND P3=2) OR (P2+A(C1) > 700)) THEN 460 ELSE P2 = P2 + A(C1): P3 = P3 -(C1=C200): GOTO 650
520 IF C1=CGAS THEN IF P0<>COOGAS THEN 460 ELSE P0=CGAS: P0 = -P0*(P8<>1) - CROLL*(P8=C25): GOTO 650
530 IF C1=CSPARE THEN IF P0<>CFLAT THEN 460 ELSE P0=CSPARE: P0 = -P0*(P8<>1) - CROLL*(P8=C25): GOTO 650
540 IF C1=CREPAIRS THEN IF P0<>CACCIDENT THEN 460 ELSE P0=CREPAIRS: P0 = -P0*(P8<>1) - CROLL*(P8=C25): GOTO 650
550 IF C1=CROLL THEN IF P0=COOGAS OR P0=CFLAT OR P0=CACCIDENT OR P0=CROLL THEN 460 ELSE P0=CROLL: GOTO 650
560 IF C1=CFLAT OR C1=COOGAS OR C1=CACCIDENT OR C1=CSTOP THEN IF (D1<>CROLL OR (C1=CFLAT AND C8=C25) OR (C1=COOGAS AND C0=C25) OR (C1=CACCIDENT AND C7=C25) OR (C1=CSTOP AND C9=C25)) THEN 460 ELSE D1=C1: GOTO 650
570 IF C1=CEOLM THEN IF R1=0 THEN 460 ELSE R1=0: GOTO 650
580 IF C1=CSLIM50 THEN IF (D2=C25 OR C9=C25) THEN 460 ELSE D2=C25: GOTO 650
590 IF C1=CXTANK THEN P0 = -P0*(P0<>COOGAS) - CROLL*(P0=COOGAS): P9=C25: GOTO 650
600 IF C1=CPPROOF THEN P0 = -P0*(P0<>CFLAT) - CROLL*(P0=CFLAT): P7=C25: GOTO 650
610 IF C1=CDRVACE THEN P0 = -P0*(P0<>CACCIDENT) - CROLL*(P0=CACCIDENT): P6=C25: GOTO 650
620 IF C1=CROWAY THEN IF P0=CSTOP OR P0=0 OR P0=CREPAIRS OR P0=CSPARE OR P0=CGAS THEN P0=CROLL
630 IF C1=CROWAY THEN R1=0: P8=C25: GOTO 650
640 GOTO 460
650 IF FNI(C1) THEN P5 = P5 + 1
660 GOTO 680
670 C1=0
680 X0=0:IF I<>7 THEN P(I)=P(7)
690 IF FNI(C1) THEN X0=CGAS: GOTO 220
700 IF P2=700 THEN T$="P": GOTO 1660
710 V$=SPACE$(79): LOCATE 9,C25: PRINT V$;V$: LOCATE 15,C25: PRINT V$: LOCATE 19,C25: PRINT V$;V$;V$
720 F=0: GOSUB 870: IF T<0 THEN 760 ELSE C(CGAS)=T: GOSUB 1300: IF T<0 THEN 720
730 IF D1=CROLL THEN GOSUB 1220: IF T<0 THEN 220 ELSE GOSUB 1210: IF T<0 THEN 220 ELSE GOSUB 890: IF T<0 THEN 220 ELSE GOSUB 940: IF T<0 THEN 220 ELSE GOSUB 1040: IF T<0 THEN 220 ELSE GOSUB 1070: IF T<0 THEN 220 ELSE 750
740 GOSUB 1210: IF T<0 THEN 220 ELSE GOSUB 1100: IF T<0 THEN 220 ELSE GOSUB 940: IF T<0 THEN 220 ELSE GOSUB 1040: IF T<0 THEN 220
750 GOSUB 1390: GOTO 220
760 X0=0: LOCATE 14,C25: COLOR FC+16: PRINT "--==>> End of Deck <<==--": COLOR FC: T$="N": GOTO 1660
770 RESTORE 780: Q0 = 101: Z3=0: READ N1: FOR Z=0 TO N1-C25: READ N2: FOR Z1=0 TO N2-C25: I(Z3+Z1) = Z+C25: NEXT Z1: Z3 = Z3 + Z1: NEXT Z
780 DATA 19: ' Nineteen card types
790 DATA 10, 10, 10, 12, 4, 2, 6, 1, 2, 6, 1, 2, 6, 1, 3, 6, 4, 14, 1
800 C3=0: P2=0: C4=0: P3=0: C5=0: P4=0: C6=0: P5=0: C8=0: P6=0: C7=0: P7=0:         C9=0: P8=0: C0=0: P9=0: D1=0: P0=0: D2=0: R1=0: D4=0: RETURN
810 FOR Z=0 TO Q0-C25: R = INT(RND*(Q0-Z)): D(Z)=I(R): IF Q0-1-Z<R THEN 830
820 FOR Z1=R TO Q0-1-Z: I(Z1)=I(Z1+1):NEXT Z1
830 NEXT Z: RETURN
840 FOR Z=C25 TO 6: GOSUB 870: IF T<0 THEN 860 ELSE P(Z)=T: GOSUB 870: IF T<0 THEN 860 ELSE C(Z)=T
850 NEXT Z: RETURN
860 PRINT: PRINT: PRINT "!!! Initial Deal Failure !!!":STOP
870 IF D4=Q0 THEN T=-C25: RETURN
880 D4=D4+C25: T=D(D4-1): RETURN
890 T=0: IF D2<>0 THEN RETURN
900 T=C200: IF C4<2 AND C3<525 THEN GOSUB 1260: IF T<0 THEN 930
910 T=C100: IF C3<625 THEN GOSUB 1260: IF T<0 THEN 930
920 RETURN
930 C3=C3+A(P1): C4 = C4 -(P1=C200): T=-C25: RETURN
940 IF P0<>CROLL THEN T=CSLIM50: IF R1=0 AND P8=0 THEN GOSUB 1260: IF T<0 THEN 1020
950 IF P0<>CROLL THEN RETURN
960 T=CFLAT: IF P7=0 THEN GOSUB 1260: IF T<0 THEN 1030
970 T=COOGAS: IF P9=0 THEN GOSUB 1260: IF T<0 THEN 1030
980 T=CACCIDENT: IF P6=0 THEN GOSUB 1260: IF T<0 THEN 1030
990 T=CSTOP: IF P8=0 THEN GOSUB 1260: IF T<0 THEN 1030
1000 T=CSLIM50: IF R1=0 AND P8=0 THEN GOSUB 1260: IF T<0 THEN 1020
1010 RETURN
1020 R1=C25: T=-C25: RETURN
1030 P0=P1: T=-C25: RETURN
1040 IF D2<>0 THEN T=CEOLM: GOSUB 1260: IF T<0 THEN 1060 ELSE T=CROWAY: GOSUB 1260: IF T<0 THEN 1060
1050 RETURN
1060 D2=0: IF P1=CROWAY THEN 1170 ELSE T=-C25: RETURN
1070 T=C75: IF D2=0 AND C3<650 THEN GOSUB 1260: IF T<0 THEN 930
1080 T=C50: IF C3<675 THEN GOSUB 1260: IF T<0 THEN 930
1090 T=C25: GOSUB 1260: IF T<0 THEN 930 ELSE RETURN
1100 IF D1=CSTOP OR D1=CREPAIRS OR D1=CGAS OR D1=CSPARE OR D1=0 THEN T=CROLL: GOSUB 1260: IF T<0 THEN 1150 ELSE T=CROWAY: GOSUB 1260: IF T<0 THEN 1160 ELSE RETURN
1110 IF D1=CACCIDENT THEN T=CDRVACE: GOSUB 1260: IF T<0 THEN 1160 ELSE T=CREPAIRS: GOSUB 1260: IF T<0 THEN 1150 ELSE RETURN
1120 IF D1=CFLAT THEN T=CPPROOF: GOSUB 1260: IF T<0 THEN 1160 ELSE T=CSPARE: GOSUB 1260: IF T<0 THEN 1150 ELSE RETURN
1130 IF D1=COOGAS THEN T=CXTANK: GOSUB 1260: IF T<0 THEN 1160 ELSE T=CGAS: GOSUB 1260: IF T<0 THEN 1150
1140 RETURN
1150 D1=P1: D1=-D1*(C9<>1) -CROLL*(C9=C25): T=-C25: RETURN
1160 D1=CROLL: IF P1=CROWAY AND D2<>0 THEN D2=0
1170 C6=C6+C25: C8 = -C8*(P1<>CPPROOF) -(P1=CPPROOF): C0 = -C0*(P1<>CXTANK) -(P1=CXTANK): C9 = -C9*(P1<>CROWAY) -(P1=CROWAY): D2 = -D2*(P1<>CROWAY): C7 = -C7*(P1<>CDRVACE) -(P1=CDRVACE)
1180 IF (D1=COOGAS AND P1=CXTANK) OR (D1=CFLAT AND P1=CPPROOF) OR (D1=CACCIDENT AND P1=CDRVACE) OR (P1=CROWAY AND (D1=CGAS OR D1=CSPARE OR D1=CREPAIRS OR D1=CSTOP)) THEN D1=CROLL
1190 T=-C25: RETURN
1200 T=CROWAY: GOSUB 1260: IF T<0 THEN 1170 ELSE T=CPPROOF: GOSUB 1260: IF T<0 THEN 1170 ELSE T=CDRVACE: GOSUB 1260: IF T<0 THEN 1170 ELSE T=CXTANK: GOSUB 1260: IF T<0 THEN 1170 ELSE RETURN
1210 T=0: IF P0<>CROLL OR P2<500 THEN RETURN ELSE IF R1<>0 AND P2<650 THEN RETURN ELSE IF P2<600 AND P2<>500 THEN RETURN ELSE IF P2=500 AND P3=2 THEN RETURN ELSE GOSUB 1200: RETURN
1220 T=0: IF D1<>CROLL OR C3<500 THEN RETURN ELSE IF D2<>0 AND C3<650 THEN RETURN ELSE IF C3<600 AND C3<>500 THEN RETURN ELSE IF C3=500 AND C4=2 THEN RETURN ELSE Z1 = 700-C3
1230 FOR Z2=C25 TO 5: IF A(Z2)=Z1 THEN 1250
1240 NEXT Z2: PRINT:PRINT:PRINT"!!! Computer Search Error !!!":STOP
1250 CT=Z1: GOSUB 1650: IF Z=0 THEN RETURN ELSE GOSUB 1200: IF T<0 THEN RETURN ELSE GOSUB 890: IF T<0 THEN RETURN ELSE GOSUB 1070: IF T<0 THEN RETURN ELSE PRINT:PRINT:PRINT "!!! Computer Play Error !!!":STOP
1260 L=6
1270 FOR I9=C25 TO L+C25: IF T=C(I9) THEN P1=T: C(I9)=C(7): T=-C25: RETURN
1280 NEXT I9: T=0: RETURN
1290 L=5: GOTO 1270
1300 IF C1=CSTOP OR C1=CSLIM50 THEN T=CROWAY: GOSUB 1290: IF T<0 THEN 1350 ELSE RETURN
1310 IF C1=COOGAS THEN T=CXTANK: GOSUB 1290: IF T<0 THEN 1350 ELSE RETURN
1320 IF C1=CFLAT THEN T=CPPROOF: GOSUB 1290: IF T<0 THEN 1350 ELSE RETURN
1330 IF C1=CACCIDENT THEN T=CDRVACE: GOSUB 1290: IF T<0 THEN 1350
1340 RETURN
1350 C5= C5+C25: C8 = -C8*(P1<>CPPROOF) -(P1=CPPROOF): C0 = -C0*(P1<>CXTANK) -(P1=CXTANK): C9 = -C9*(P1<>CROWAY) -(P1=CROWAY): C7 = -C7*(P1<>CDRVACE) -(P1=CDRVACE):LOCATE 9,C25:COLOR 16+FC:PRINT "!!! Coup Fourr"CHR$(130)" !!!": F=C25:COLOR FC
1360 T=P1: C6=C6+C25: IF P1=CROWAY THEN IF D2<>0 THEN D2=0: IF D1=CACCIDENT OR D1=COOGAS OR D1=CFLAT THEN 1380
1370 D1=CROLL
1380 T=-C25: RETURN
1390 IF P6=C25 THEN T=CACCIDENT: GOSUB 1260: IF T<0 THEN 1640
1400 IF P9=C25 THEN T=COOGAS: GOSUB 1260: IF T<0 THEN 1640
1410 IF P7=C25 THEN T=CFLAT: GOSUB 1260: IF T<0 THEN 1640
1420 IF P8=C25 THEN T=CSLIM50: GOSUB 1260: IF T<0 THEN 1640 ELSE T=CSTOP: GOSUB 1260: IF T<0 THEN 1640
1430 IF C3>500 THEN T=C200: GOSUB 1260: IF T<0 THEN 1640
1440 IF C3>600 THEN T=C100: GOSUB 1260: IF T<0 THEN 1640
1450 IF C3>625 THEN T=C75: GOSUB 1260: IF T<0 THEN 1640
1460 IF C3>650 THEN T=C50: GOSUB 1260: IF T<0 THEN 1640
1470 CT=C200: GOSUB 1650: IF Z>2 THEN T=C200: GOSUB 1260: IF T<0 THEN 1640
1480 CT=CDRVACE: GOSUB 1650: IF Z<>0 OR C7=C25 THEN T=CREPAIRS: GOSUB 1260: IF T<0 THEN 1640
1490 CT=CXTANK: GOSUB 1650: IF Z<>0 OR C0=C25 THEN T=CGAS: GOSUB 1260: IF T<0 THEN 1640
1500 CT=CPPROOF: GOSUB 1650: IF Z<>0 OR C8=C25 THEN T=CSPARE: GOSUB 1260: IF T<0 THEN 1640
1510 CT=CROWAY: GOSUB 1650: IF Z<>0 OR C9=C25 THEN T=CEOLM: GOSUB 1260: IF T<0 THEN 1640 ELSE T=CROLL: GOSUB 1260: IF T<0 THEN 1640
1520 T=C25: GOSUB 1260: IF T<0 THEN 1640 ELSE T=C50: GOSUB 1260: IF T<0 THEN 1640 ELSE T=C75: GOSUB 1260: IF T<0 THEN 1640
1530 CT=CEOLM: GOSUB 1650: IF Z>1 THEN T=CEOLM: GOSUB 1260: IF T<0 THEN 1640
1540 CT=CREPAIRS: GOSUB 1650: IF Z>1 THEN T=CREPAIRS: GOSUB 1260: IF T<0 THEN 1640
1550 CT=CSPARE: GOSUB 1650: IF Z>1 THEN T=CSPARE: GOSUB 1260: IF T<0 THEN 1640
1560 CT=CGAS: GOSUB 1650: IF Z>1 THEN T=CGAS: GOSUB 1260: IF T<0 THEN 1640
1570 CT=CROLL: GOSUB 1650: IF Z>1 THEN T=CROLL: GOSUB 1260: IF T<0 THEN 1640
1580 T=C100: GOSUB 1260: IF T<0 THEN 1640
1590 T=C200: GOSUB 1260: IF T<0 THEN 1640
1600 IF FNI(C(7))<>1 THEN 1630
1610 FOR Z=C25 TO 6: IF FNI(C(Z))=0 THEN T=C(Z): GOSUB 1260: IF T<0 THEN 1640
1620 NEXT Z: PRINT:PRINT:PRINT "!!! Safety Discard Error !!!":STOP
1630 P1=C(7)
1640 LOCATE 10,C25: PRINT "Computer discards a"D$(P1): T=0: F=C25: RETURN
1650 Z=0: FOR Z1=C25 TO 6: Z=Z-(C(Z1)=CT): NEXT Z1: RETURN
1660 F=C25: X0=0: X$=T$: GOSUB 1770: LOCATE 24,C25: COLOR 16+FC: PRINT "Press a key for score ";: V$=INPUT$(C25): COLOR FC: CLS
1670 F$=STRING$(6,"#"): F1$=STRING$(8, "#"): PRINT:PRINT:PRINT,, "Player", "Computer": PRINT "Previous Total",:PRINT USING F$; O9;:PRINT,:PRINT USING F1$; A9:PRINT "Mile Stones",,:PRINT USING F$; P2;:PRINT,:PRINT USING F1$; C3:O9=O9+P2: A9=A9+C3
1680 Z1=P5*100: Z2=C6*100: Z1=Z1-300*(Z1=400): Z2=Z2-300*(Z2=400):PRINT "Safeties",,: PRINT USING F$; Z1;:PRINT,: PRINT USING F1$; Z2
1690 O9=O9+Z1: A9=A9+Z2: PRINT "Coup Fourr"CHR$(130)"s",,:PRINT USING F$; P4*300;:PRINT,: PRINT USING F1$; C5*300: O9=O9+P4*300: A9=A9+C5*300
1700 Z1=0: Z2=0: Z3=0: Z4=0: Z5=0: Z6=0
1710 PRINT "Trip Completed",: IF X$="P" THEN Z1=400: Z2=-300*(P3=0): Z3=-500*(C3=0)
1720 IF X$="C" THEN Z4=400: Z5=-300*(C4=0): Z6=-500*(P2=0)
1730 PRINT USING F$; Z1;:PRINT,:PRINT USING F1$; Z4: PRINT "Safe Trip",,:PRINT USING F$; Z2;:PRINT,:PRINT USING F1$; Z5:PRINT "Shut Out",,:PRINT USING F$; Z3;:PRINT,:PRINT USING F1$; Z6:O9=O9+Z1+Z2+Z3:A9=A9+Z4+Z5+Z6
1740 PRINT "Current Total",,:PRINT USING F$; O9;:PRINT,:PRINT USING F1$; A9:IF (O9>=5000 OR A9>=5000) AND O9<>A9 THEN 1910
1750 PRINT: PRINT "Press ENTER for next hand ";: LINE INPUT V$: GOTO 210
1760 V$=INPUT$(C25): VV=ASC(V$): V$=CHR$(VV +32*(VV>95)):RETURN
1770 LOCATE C25,C25:PRINT " Player Status  Computer Status    Player's Hand"
1780 PRINT A$(P0)"   "A$(D1)"   "A$(P(C25))" (1)": PRINT A$(16+(R1<>0))"   "A$(16+(D2<>0))"   "A$(P(2))" (2)": PRINT USING STRING$(14, "#")+"   "; P2;: PRINT USING STRING$(14, "#")+"   "; C3;: PRINT A$(P(3))" (3)"
1790 S(C25)=C25: S(2)=C25: S(3)=C25: S(4)=C25
1800 IF P5=4 OR C6=4 THEN Z=4 ELSE Z=0
1810 FOR Z1=C25 TO 3-(Z>3): T=C25: IF (P6 AND S(C25)) THEN PRINT A$(14)"   ";: S(C25)=0: T=0
1820 IF (P7 AND S(2) AND T) THEN PRINT A$(11)"   ";: S(2)=0: T=0
1830 IF (P8 AND S(3) AND T) THEN PRINT A$(19)"   ";: S(3)=0: T=0
1840 IF (P9 AND S(4) AND T) THEN PRINT A$(8)"   ";: S(4)=0: T=0
1850 PRINT SPACE$(T*17);: T=C25: IF (C7 AND S(C25)) THEN PRINT A$(14)"   ";: S(C25)=0: T=0
1860 IF (C8 AND S(2) AND T) THEN PRINT A$(11)"   ";: S(2)=0: T=0
1870 IF (C9 AND S(3) AND T) THEN PRINT A$(19)"   ";: S(3)=0: T=0
1880 IF (C0 AND S(4) AND T) THEN PRINT A$(8)"   ";: S(4)=0: T=0
1890 PRINT SPACE$(T*17);: IF Z1<4 THEN PRINT A$(P(3+Z1))" (";: PRINT USING "#"; 3+Z1;: PRINT ")": ELSE PRINT
1900 NEXT Z1: PRINT: RETURN
1910 LOCATE 20,C25:IF O9>A9 THEN PRINT "Congratulations, you win!" ELSE PRINT "Better luck next time - I win."
1920 CHAIN"menu",10000
1930 END
